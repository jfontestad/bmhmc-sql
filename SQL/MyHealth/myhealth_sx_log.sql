/*
***********************************************************************
File: myhealth_sx_log.sql

Input Parameters:
	DECLARE @BMH_START_1    DATETIME;
    DECLARE @BMH_END_1      DATETIME;
    DECLARE @BMH_START_2    DATETIME;
    DECLARE @BMH_END_2      DATETIME;
    DECLARE @ORSOS_START_1  DATETIME;
    DECLARE @ORSOS_END_1    DATETIME;
    DECLARE @ORSOS_START_2  DATETIME;
    DECLARE @ORSOS_END_2    DATETIME;

Tables/Views:
	[BMH-ORSOS].[ORSPROD].[ORSPROD].[POST_CASE]
	[BMH-ORSOS].[ORSPROD].[ORSPROD].[PROCEDURES]
	[BMH-ORSOS].[ORSPROD].[ORSPROD].[CLINICAL]
	[BMH-ORSOS].[ORSPROD].[ORSPROD].[POST_ANES_TYPE]
    [BMH-ORSOS].[ORSPROD].[ORSPROD].[POST_RESOURCE]
    [BMH-ORSOS].[ORSPROD].[ORSPROD].[CODES_ROLE]

Creates Table:
	None

Functions:
	None

Author: Steven P Sanderson II, MPH

Department: Finance, Revenue Cycle

Purpose/Description
	Monthly MyHealth Sx Log File

Revision History:
Date		Version		Description
----		----		----
2019-10-23	v1			Initial Creation
***********************************************************************
*/

DECLARE @BMH_START_1 DATETIME;
DECLARE @BMH_END_1 DATETIME;
DECLARE @BMH_START_2 DATETIME;
DECLARE @BMH_END_2 DATETIME;
DECLARE @ORSOS_START_1 DATETIME;
DECLARE @ORSOS_END_1 DATETIME;
DECLARE @ORSOS_START_2 DATETIME;
DECLARE @ORSOS_END_2 DATETIME;

SET @ORSOS_START_1 = '2019-01-01 00:00:00';
SET @ORSOS_END_1 = '2019-02-01 00:00:00';
SET @BMH_START_1 = @ORSOS_START_1;
SET @BMH_END_1 = @ORSOS_END_1;
SET @ORSOS_START_2 = '2021-01-01 00:00:00';
SET @ORSOS_END_2 = '2021-08-01 00:00:00';
SET @BMH_START_2 = @ORSOS_START_2;
SET @BMH_END_2 = @ORSOS_END_2;

---------------------------------------------------------------------------------------------------
-- G E T - O R - R O O M - C A S E S - F R O M - O R S O S                                        |
---------------------------------------------------------------------------------------------------
DECLARE @ORSOS_OR_RM_TBL TABLE (
	[ORSOS Case No] VARCHAR(10),
	[DSS Case No] VARCHAR(15),
	[ORSOS MD ID] VARCHAR(15),
	[Provider Name] VARCHAR(MAX),
	[ORSOS Room ID] VARCHAR(50),
	[Incision_Date] DATE,
	[Incision_Time] TIME(0),
	[Closure_Date] DATE,
	[Closure_Time] TIME(0),
	[Ent Proc Rm Time] TIME(0),
	[Leave Proc Rm Time] TIME(0),
	[Procedure] VARCHAR(MAX),
	[Anes Start Date] DATE,
	[Anes Start Time] TIME(0),
	[Anes End Date] DATE,
	[Anes End Time] TIME(0),
	[Patient Type] VARCHAR(5),
	[Adm Recovery Date] DATE,
	[Adm Recovery Time] TIME(0),
	[Leave Recovery Date] DATE,
	[Leave Recovery Time] TIME(0)
	);

WITH CTE
AS (
	SELECT A.CASE_NO,
		C.FACILITY_ACCOUNT_NO,
		E.RESOURCE_ID,
		A.PROVIDER_SHORT_NAME,
		A.ROOM_ID,
		CAST(A.START_DATE AS DATE) AS [INCISION_DATE],
		CAST(A.START_TIME AS TIME(0)) AS [INCISION_TIME],
		CAST(A.STOP_DATE AS DATE) AS [CLOSURE_DATE],
		CAST(a.STOP_TIME AS TIME(0)) AS [CLOSURE_TIME],
		CAST(A.ENTER_PROC_ROOM_TIME AS TIME(0)) AS [ENTER_PROC_ROOM_TIME],
		CAST(A.LEAVE_PROC_ROOM_TIME AS TIME(0)) AS [LEAVE_PROC_ROOM_TIME],
		B.[DESCRIPTION] AS PROCEDURE_DESCRIPTION,
		CAST(D.ANES_START_DATE AS DATE) AS [ANES_START_DATE],
		CAST(D.ANES_START_TIME AS TIME(0)) AS [ANES_START_TIME],
		CAST(D.ANES_STOP_DATE AS DATE) AS [ANES_STOP_DATE],
		CAST(D.ANES_STOP_TIME AS TIME(0)) AS [ANES_STOP_TIME],
		C.PATIENT_TYPE,
		CAST(A.ADMIT_RECOVERY_DATE AS DATE) AS [ADMIT_RECOVERY_DATE],
		CAST(A.ADMIT_RECOVERY_TIME AS TIME(0)) AS [ADMIT_RECOVERY_TIME],
		CAST(A.LEAVE_RECOVERY_DATE AS DATE) AS [LEAVE_RECOVERY_DATE],
		CAST(A.LEAVE_RECOVERY_TIME AS TIME(0)) AS [LEAVE_RECOVERY_TIME]
	FROM (
		(
			[BMH-ORSOS].[ORSPROD].[ORSPROD].[POST_CASE] AS A INNER JOIN [BMH-ORSOS].[ORSPROD].[ORSPROD].[PROCEDURES] AS B ON A.MAIN_PROCEDURE_ID = B.PROCEDURE_ID
			) INNER JOIN [BMH-ORSOS].[ORSPROD].[ORSPROD].[CLINICAL] AS C ON A.ACCOUNT_NO = C.ACCOUNT_NO
		)
	LEFT OUTER JOIN [BMH-ORSOS].[ORSPROD].[ORSPROD].[POST_ANES_TYPE] AS D ON A.CASE_NO = D.CASE_NO
	LEFT OUTER JOIN [BMH-ORSOS].[ORSPROD].[ORSPROD].[POST_RESOURCE] AS E ON A.CASE_NO = E.CASE_NO
		AND E.ROLE_CODE = '1'
	LEFT OUTER JOIN [BMH-ORSOS].[ORSPROD].[ORSPROD].[CODES_ROLE] AS F ON E.ROLE_CODE = F.CODE
	WHERE (
			A.DELETE_FLAG IS NULL
			OR (
				A.DELETE_FLAG = ''
				OR A.DELETE_FLAG = 'Z'
				)
			)
		AND (
			(
				A.START_DATE >= @ORSOS_START_1
				AND A.START_DATE < @ORSOS_END_1
				)
			OR (
				A.START_DATE >= @ORSOS_START_2
				AND A.START_DATE < @ORSOS_END_2
				)
			)
		AND RIGHT(C.FACILITY_ACCOUNT_NO, 1) != 'J'
		AND E.RESOURCE_ID IN ('00593', '014241')
	)
INSERT INTO @ORSOS_OR_RM_TBL
SELECT *
FROM CTE;

SELECT *
FROM @ORSOS_OR_RM_TBL
ORDER BY [ORSOS MD ID],
	[Incision_Date]


